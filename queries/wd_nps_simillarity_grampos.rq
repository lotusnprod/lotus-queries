#+ summary: Natural products structurally similar to activity
#+ description: What are the parent taxa containing the most compounds similar to compounds known to treat gram-positive infections (Q55789782)?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products
#+   - Structural similarity

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: What are the parent taxa containing the most compounds similar to compounds known to treat gram-positive infections (Q55789782)?
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>
SELECT DISTINCT ?parent_taxon ?parent_taxon_name (COUNT(DISTINCT ?compound) AS ?count) WHERE {
    SERVICE idsm:wikidata {
        VALUES ?CUTOFF {
            "0.85"^^xsd:double
        }
        SERVICE <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
            VALUES ?disease {
                wd:Q55789782
            }
            ?infection (wdt:P279*) ?disease.
            { ?structure wdt:P2175 ?infection. }
            UNION
            { ?infection wdt:P2176 ?structure. }
            ?structure wdt:P233 ?structure_smiles;
            wdt:P703 ?taxon.
        }
        ?compound sachem:similarCompoundSearch [
        sachem:query ?structure_smiles;
        sachem:cutoff ?CUTOFF
        ].
    } hint:Prior hint:runFirst TRUE.
    ?compound wdt:P703/wdt:P171 ?parent_taxon.
    ?parent_taxon wdt:P225 ?parent_taxon_name.
}
GROUP BY ?parent_taxon ?parent_taxon_name
ORDER BY DESC (?count)