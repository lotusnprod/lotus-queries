#+ summary: Chemical scaffolds found in taxa (indole)
#+ description: Which organisms contain indolic scaffolds? Count occurences, group and order the results by the parent taxon
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Chemical scaffolds

PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: Which organisms contain at least 100 indolic scaffolds? Results ordered by parent taxon.
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
# prefixes needed for structural similarity search
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>
SELECT ?parent_taxon ?parent_taxon_name (COUNT(DISTINCT ?compound) AS ?count) WHERE {
    SERVICE idsm:wikidata {
        VALUES ?SUBSTRUCTURE {
            "C12=C(C=CC=C2)C=CN1" # indolic scaffold
        }
        ?compound sachem:substructureSearch [
        sachem:query ?SUBSTRUCTURE
        ].
    } hint:Prior hint:runFirst TRUE.
    ?compound p:P703 ?statement.
    ?statement ps:P703/wdt:P171 ?parent_taxon.
    ?parent_taxon wdt:P225 ?parent_taxon_name.
}
GROUP BY ?parent_taxon ?parent_taxon_name
HAVING (?count > 100)
ORDER BY DESC (?count)
