#+ summary: Natural products with bioactivity against Gram-positive infections
#+ description: What are the compounds found in taxon known to treat gram-positive infections (Q55789782)?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#title: What are the compounds found in taxon known to treat gram-positive infections (Q55789782)?
SELECT DISTINCT ?infection ?infectionLabel ?structure ?structure_smiles ?taxon ?taxon_name WHERE {
    VALUES ?disease {
        wd:Q55789782   # Gram-positive
    # wd:Q19597369 # Gram-negative
    }
    ?infection (wdt:P279*) ?disease.
    { ?structure wdt:P2175 ?infection. }
    UNION
    { ?infection wdt:P2176 ?structure. }
    ?structure wdt:P233 ?structure_smiles;
    wdt:P703 ?taxon.
    ?taxon wdt:P225 ?taxon_name.
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
