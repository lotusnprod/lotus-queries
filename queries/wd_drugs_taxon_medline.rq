#+ summary: Drugs found in taxa
#+ description: What are the drugs (identified in Medline) found in taxa?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Drugs

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
#title: What are the drugs (identified in Medline) found in taxa?
SELECT DISTINCT ?drug ?inchikey ?medlineDrugId WHERE {
    ?drug wdt:P235 ?inchikey;
    wdt:P10245 ?medlineDrugId;
    p:P703[
    ps:P703 ?taxon;
    (prov:wasDerivedFrom/pr:P248) ?reference ].
    FILTER (?taxon != wd:Q15978631) # exclude drugs found in Homo sapiens
}
