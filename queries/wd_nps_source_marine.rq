#+ summary: Natural products from marine sources (with references)
#+ description: What are the chemical compounds found in marine organisms and the articles reporting them?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: What are the chemical compounds found in marine organisms and the articles reporting them?
SELECT DISTINCT ?structure ?structure_smiles ?reference ?reference_title ?reference_doi WHERE {
    {
        SELECT ?child WHERE {
            { ?child (wdt:P171*) wd:Q152. }    # fishes
            UNION
            { ?child (wdt:P171*) wd:Q93315. }  # cyanobacteria
            UNION
            { ?child (wdt:P171*) wd:Q103169. } # red algae
            UNION
            { ?child (wdt:P171*) wd:Q264543. } # chlorohyta
            UNION
            { ?child (wdt:P171*) wd:Q18960. }  # sponges
            UNION
            { ?child (wdt:P171*) wd:Q28524. }  # corals
        }
    }
    hint:Prior hint:runFirst TRUE.
    ?structure wdt:P233 ?structure_smiles;
    p:P703[
    ps:P703 ?child;
    (prov:wasDerivedFrom/pr:P248) ?reference ].
    ?reference wdt:P356 ?reference_doi;
    wdt:P1476 ?reference_title.
}
