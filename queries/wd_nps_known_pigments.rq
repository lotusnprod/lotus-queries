#+ summary: Natural products known as pigments
#+ description: Which pigments are found in which taxa, according to which reference?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
#title: Which pigments are found in which taxa, according to which reference?
# special thanks goes to User:Lmichan for updating this information!
SELECT DISTINCT ?compound ?compoundLabel ?taxon ?taxonname ?DOI WHERE {
    ?compound p:P703 ?P703statement;
    ((wdt:P31*)/(wdt:P279*)) wd:Q161179.
    ?P703statement ps:P703 ?taxon;
    (prov:wasDerivedFrom/pr:P248) _:b26.
    _:b26 wdt:P356 ?DOI.
    ?taxon wdt:P225 ?taxonname.
    ?compound rdfs:label ?compoundLabel.
    FILTER((LANG(?compoundLabel)) = "en")
}
ORDER BY (?compoundLabel)
LIMIT 10000
