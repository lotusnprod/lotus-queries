#+ summary: Natural products with bioactivity in Actinomycetes
#+ description: Which natural products with known bioactivities were isolated from Actinomycetes (Q62606918), between 2014 and 2019, with related organisms and references?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
#title: Which natural products with known bioactivities were isolated from Actinomycetes (Q62606918), between 2014 and 2019, with related organisms and references?
SELECT ?organism ?organism_name ?compound ?compound_smiles ?bioactivity ?isolation_reference ?reference_title WHERE {
    ?organism (wdt:P171*) wd:Q62606918;
    wdt:P225 ?organism_name.
    ?compound wdt:P235 ?compound_id;
    wdt:P233 ?compound_smiles;
    p:P703 ?statement;
    (wdt:P2868/wdt:P486) ?meshId.
    ?mesh wdt:P486 ?meshId;
    rdfs:label ?bioactivity.
    FILTER(LANGMATCHES(LANG(?bioactivity), "EN"))
    ?statement ps:P703 ?organism;
    prov:wasDerivedFrom ?ref.
    ?ref pr:P248 ?isolation_reference.
    ?isolation_reference wdt:P1476 ?reference_title;
    wdt:P577 ?reference_date.
    FILTER(((YEAR(?reference_date)) >= 2014 ) && ((YEAR(?reference_date)) <= 2019 ))
}
LIMIT 10000
