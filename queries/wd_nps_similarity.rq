#+ summary: Natural products structurally similar to chemicals
#+ description: How many compounds are structurally similar to eugenol and eugenyl acetate? Results are grouped by the parent taxon of the organism they were found in.
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products
#+   - Structural similarity

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title:How many compounds are structurally similar to eugenol and eugenyl acetate? Results are grouped by the parent taxon of the organism they were found in.
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
# prefixes needed for structural similarity search
PREFIX idsm: <https://idsm.elixir-czech.cz/sparql/endpoint/>
SELECT ?parent_taxon ?parent_taxon_name (COUNT(DISTINCT ?compound) AS ?count) WHERE {
    SERVICE idsm:wikidata {
        VALUES ?CUTOFF {
            "0.85"^^xsd:double
        }
        SERVICE <https://query.wikidata.org/bigdata/namespace/wdq/sparql> {
            VALUES ?derivatives {
                wd:Q423357
                wd:Q27116129
            }
            ?derivatives wdt:P233 ?smiles.
        }
        ?compound sachem:similarCompoundSearch [
        sachem:query ?smiles;
        sachem:cutoff ?CUTOFF
        ].
    } hint:Prior hint:runFirst TRUE.
    ?compound wdt:P703/wdt:P171 ?parent_taxon.
    ?parent_taxon wdt:P225 ?parent_taxon_name.
}
GROUP BY ?parent_taxon ?parent_taxon_name
ORDER BY DESC (?count)
