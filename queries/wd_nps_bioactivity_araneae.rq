#+ summary: Natural products with bioactivity in spiders (Araneae)
#+ description: What are the biological activities of compounds found in Araneae (Q1357) or children taxa?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
#title: What are the biological activities of compounds found in Araneae (Q1357) or children taxa?
SELECT DISTINCT ?structure ?structure_inchikey (GROUP_CONCAT(DISTINCT ?meshLabel; SEPARATOR = "|") AS ?bioactivities) WHERE {
    VALUES ?taxon {
        wd:Q1357
    }
    ?structure wdt:P235 ?structure_inchikey;
    wdt:P703/(wdt:P171*) ?taxon;
    (wdt:P2868/wdt:P486) ?meshId.
    ?mesh wdt:P486 ?meshId;
    rdfs:label ?meshLabel.
    FILTER(LANG(?meshLabel) = "en")
}
GROUP BY ?structure ?structure_inchikey
LIMIT 100
