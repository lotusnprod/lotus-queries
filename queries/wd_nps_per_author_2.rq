#+ summary: Natural products per author 2
#+ description: Which authors isolated the most natural products (based on a sample of 10,000 compounds)? Return the top 30 authors by count.
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products
#+   - Authors

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX hint: <http://www.bigdata.com/queryHints#>
#title: Which authors isolated the most natural products (based on a sample of 10,000 compounds)? Return the top 30 authors by count.
#defaultView:BubbleChart
SELECT ?authorLabel ?nationalityLabel ?rgb (COUNT(DISTINCT ?compound) AS ?count) WHERE {
    {
        SELECT DISTINCT ?compound ?art WHERE {
            {
                SELECT DISTINCT ?compound WHERE {
                    ?compound wdt:P235 ?inchikey.
                    SERVICE bd:sample {
                        ?compound wdt:P703 ?taxon_with_compound.
                        bd:serviceParam bd:sample.limit 10000 .
                    }
                }
            }
            hint:Prior hint:runFirst "true".
            ?compound p:P703 ?stmt.
            ?stmt (prov:wasDerivedFrom/pr:P248) ?art.
        }
    }
    hint:Prior hint:runFirst TRUE.
    ?art wdt:P50 ?author.
    ?author wdt:P21 ?gender;
    wdt:P27 ?nationality.
    BIND(IF(?gender = wd:Q6581097, "2994D2", "D71D62") AS ?rgb)
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
GROUP BY ?authorLabel ?nationalityLabel ?rgb
ORDER BY DESC (?count)
LIMIT 30