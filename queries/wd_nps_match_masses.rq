#+ summary: Natural products corresponding to given masses
#+ description: Which compounds corresponding to a given mass ± 10ppm are found in which organisms?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#title: Which compounds corresponding to a given mass ± 10ppm are found in which organism(s)?
SELECT DISTINCT ?compound ?mf ?inchi (GROUP_CONCAT(?taxon_name; SEPARATOR = ", ") AS ?organism) WHERE {
    VALUES ?query {
        "524.1765"^^xsd:decimal
    }
    VALUES ?ppm {
        "10"^^xsd:decimal
    }
    ?compound wdt:P2067 ?mass.
    FILTER((?mass > (?query - ((?ppm * "0.000001"^^xsd:decimal) * ?query))) && (?mass < (?query + ((?ppm * "0.000001"^^xsd:decimal) * ?query))))
    ?compound (wdt:P703/wdt:P225) ?taxon_name;
    wdt:P234 ?inchi;
    wdt:P274 ?mf.
}
GROUP BY ?compound ?mf ?inchi
LIMIT 10000
