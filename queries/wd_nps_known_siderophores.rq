#+ summary: Natural products known as siderophores
#+ description: Which are the available siderophores found in organisms on Wikidata? (example limited to 1000 results)
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pr: <http://www.wikidata.org/prop/reference/>
PREFIX prov: <http://www.w3.org/ns/prov#>
#title: Which are the available siderophores found in organisms on Wikidata? (example limited to 1000 results)
SELECT DISTINCT ?structure ?structure_smiles ?structure_inchikey ?taxon ?taxon_name ?reference ?reference_doi WHERE {
    VALUES ?siderophores {
        wd:Q424747   # siderophores
        wd:Q50349216 # iron chelating agent
        wd:Q71429918 # iron ionophore
    }
    ?structure wdt:P235 ?structure_inchikey;    # get the inchikey
    wdt:P233 ?structure_smiles;                 # get the smiles
    wdt:P2868 ?siderophores;
    p:P703[                                     # statement found in taxon
    ps:P703 ?taxon;                             # get the taxon
    (prov:wasDerivedFrom/pr:P248) ?reference ]. # get the reference
    ?taxon wdt:P225 ?taxon_name.                # get the taxon scientific name
    ?reference wdt:P356 ?reference_doi.         # get the reference DOI
}
LIMIT 1000
