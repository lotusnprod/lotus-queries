#+ summary: Natural products from qid
#+ description: What are the compounds found in Mouse-ear cress Arabidopsis thaliana (Q158695) or children taxa?
#+ endpoint: https://query.wikidata.org/bigdata/namespace/wdq/sparql
#+ pagination: 100
#+ method: GET
#+ tags:
#+   - Natural products

PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
#title: What are the compounds found in Mouse-ear cress Arabidopsis thaliana (Q158695) or children taxa?
SELECT DISTINCT ?structure ?structureLabel ?structure_inchikey WHERE {
    VALUES ?taxon {
        wd:Q158695                             # You can remove the Qxxxxxx and hit Ctrl+space, type the first letters and it should autocomplete
    }
    ?children (wdt:P171*) ?taxon.              # Include children taxa
    ?structure wdt:P235 ?structure_inchikey;   # Get the InChIKey
    wdt:P703 ?children.                        # Found in given taxon/taxa
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}
LIMIT 10000
